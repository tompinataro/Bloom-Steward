rootProject.name = 'Bloom Steward'

def findNodeModulesFile(String relativePath) {
  def current = rootDir
  while (current != null) {
    def candidate = new File(current, "node_modules/${relativePath}")
    if (candidate.exists()) {
      return candidate
    }
    current = current.getParentFile()
  }
  return null
}

def rnCliGradle = findNodeModulesFile("@react-native-community/cli-platform-android/native_modules.gradle")
apply from: rnCliGradle
applyNativeModulesSettingsGradle(settings)

def expoModulesPath = new File(
  ["node", "--print", "require.resolve('expo-modules-autolinking/package.json')"].execute(null, rootDir).text.trim()
).getParentFile().absolutePath
apply from: new File(expoModulesPath, "scripts/android/autolinking_implementation.gradle")
useExpoModules()

def rnGradlePlugin = findNodeModulesFile("@react-native/gradle-plugin")

include ':app'
includeBuild(rnGradlePlugin.absolutePath)
